; Script generated by the HM NIS Edit Script Wizard.

!include LogicLib.nsh
!include "x64.nsh"
!define PRODUCT_NAME "MCM6000"
!define PRODUCT_VERSION "1.4"
!define MAIN_FOLDER_NAME "70-0079-1.4"
!define MAJOR_VERSION "1.4"
!define APP_NAME "MesoScopeUI"
!define PRODUCT_PUBLISHER "Thorlabs"
!define PRODUCT_WEB_SITE "https://www.thorlabs.com"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\${APP_NAME}.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}_${MAJOR_VERSION}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"
!define PRODUCT_FOLDER_NAME "${PRODUCT_NAME} ${MAJOR_VERSION}"
!echo "Outer invocation"
; Execute the Temporary installer created from Device_Uninstaller_Script.nsi which will create an uninstaller
!system "$%TEMP%\temp_${PRODUCT_NAME}.exe" = 2
; Sign the uninstaller
!system "SignTool sign /f SignKey\key.pfx /p Shenzhen /t http://timestamp.verisign.com/scripts/timestamp.dll $%TEMP%\uninst_${PRODUCT_NAME}_${MAJOR_VERSION}.exe" = 0
!system "SignTool sign /f SignKey\key.pfx /p Shenzhen /t http://timestamp.verisign.com/scripts/timestamp.dll ${MAIN_FOLDER_NAME}\${APP_NAME}.exe" = 0
!system "SignTool sign /f SignKey\key.pfx /p Shenzhen /t http://timestamp.verisign.com/scripts/timestamp.dll ${MAIN_FOLDER_NAME}\ThemeControl.dll" = 0
; HM NIS Edit Wizard helper defines

SetCompressor /SOLID lzma

; MUI 1.67 compatible. 'Modern UI' is the Theme ------
!include "MUI.nsh"
!include "WinVer.nsh"
!include "WordFunc.nsh"
  !insertmacro VersionCompare

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
; !insertmacro MUI_PAGE_LICENSE "License.rtf"
; Components page
;!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\${APP_NAME}.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI end ------

Name "${PRODUCT_PUBLISHER} ${PRODUCT_NAME} ${MAJOR_VERSION}"
OutFile "${MAIN_FOLDER_NAME}_Installer.exe"
InstallDir "$PROGRAMFILES64\Thorlabs\${PRODUCT_FOLDER_NAME}"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "Application" SEC01
  SectionIn RO
  SetOverwrite try
  SetOutPath "$INSTDIR"
  File /r "${MAIN_FOLDER_NAME}\${APP_NAME}.exe"
  File /r "${MAIN_FOLDER_NAME}\ThemeControl.dll"
  File /r "${MAIN_FOLDER_NAME}\NDP452-KB2901954-Web.exe"
  SetOutPath "$INSTDIR\Drivers"
  File /r "${MAIN_FOLDER_NAME}\Drivers\*"
 
  ; Read the version of Windows and call driver installer if it is not Windows 10. Then call the dotnet and redist installers (not necessary if drivers are signed)
  ;${WinVerGetMajor} $0
  ;${If} $0 != "10"
  ;Check if it is 32bit or 64bit
  ${If} ${RunningX64}
    ExecWait "$INSTDIR\Drivers\dpinst64.exe /sa /p"
  ${Else}
    ExecWait "$INSTDIR\Drivers\dpinst32.exe /sa /p"
  ${EndIf}
  Delete "$INSTDIR\Drivers\dpinst64.exe"
  Delete "$INSTDIR\Drivers\dpinst32.exe"
  ;${Else}
    ;DetailPrint "It's not necessary to install the drivers for this device in Windows 10. Skipping dpinst64.exe"
  ;${EndIf}
  Call IsDotNetInstalled
  ; Grant FullAccess permission for all users to the Application folder, mostly to read and write the settings file with no errors.
  ;AccessControl::GrantOnFile  "$INSTDIR\Application" "(S-1-5-32-545)" "FullAccess"
SectionEnd

Section -AdditionalIcons
  SetOutPath "$INSTDIR" ;SetOutPath to the path where the shotrcuts are going to 'Start In'
  CreateDirectory "$SMPROGRAMS\Thorlabs\${PRODUCT_FOLDER_NAME}"
  CreateShortCut "$SMPROGRAMS\Thorlabs\${PRODUCT_FOLDER_NAME}\${PRODUCT_NAME} ${MAJOR_VERSION}.lnk" "$INSTDIR\${APP_NAME}.exe"
  CreateShortCut "$DESKTOP\${PRODUCT_NAME} ${MAJOR_VERSION}.lnk" "$INSTDIR\${APP_NAME}.exe"
  WriteIniStr "$INSTDIR\${PRODUCT_PUBLISHER}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\Thorlabs\${PRODUCT_FOLDER_NAME}\Website.lnk" "$INSTDIR\${PRODUCT_PUBLISHER}.url"
  CreateShortCut "$SMPROGRAMS\Thorlabs\${PRODUCT_FOLDER_NAME}\Uninstall.lnk" "$INSTDIR\uninst_${PRODUCT_NAME}_${MAJOR_VERSION}.exe"
SectionEnd

Section -Post
  SetOutPath $INSTDIR
  ; this packages the signed uninstaller
  File $%TEMP%\uninst_${PRODUCT_NAME}_${MAJOR_VERSION}.exe
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\${APP_NAME}.exe"
  WriteRegStr HKLM "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr HKLM "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst_${PRODUCT_NAME}_${MAJOR_VERSION}.exe"
  WriteRegStr HKLM "${PRODUCT_UNINST_KEY}" "InstallLocation" "$INSTDIR"
  WriteRegStr HKLM "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\${APP_NAME}.exe"
  WriteRegStr HKLM "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr HKLM "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr HKLM "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC01} "${PRODUCT_NAME} Control App and Drivers"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC02} "Software Development Kit files"
!insertmacro MUI_FUNCTION_DESCRIPTION_END

Function .onInit
  ;Check if the computer is running a 32 bit or 64 bit version of Windows
  ;${If} ${RunningX64}
    ;Do nothing
  ;${ElseIf} ${PRODUCT_NAME} == "CSN210"
    ;Do nothing
  ;${Else}
     ;MessageBox MB_OK|MB_ICONEXCLAMATION "Cannot install 64-bit version of ${PRODUCT_FOLDER_NAME} because you have 32-bit version of Windows installed"
	  ;Quit
  ;${EndIf}
  ;Rewrite the default instdir for the installer, otherwise it will map to \Application by default 
  StrCpy $INSTDIR "$PROGRAMFILES64\Thorlabs\${PRODUCT_FOLDER_NAME}"
  ReadRegStr $R0 HKLM "${PRODUCT_UNINST_KEY}" "UninstallString"
  ReadRegStr $R1 HKLM "${PRODUCT_UNINST_KEY}" "InstallLocation"
  ReadRegStr $R2 HKLM "${PRODUCT_UNINST_KEY}" "DisplayVersion"
  StrCmp $R0 "" done
  MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION \
  "${PRODUCT_FOLDER_NAME} is already installed. $\n$\nClick `OK` to remove the \
  previous version or `Cancel` to cancel this upgrade." \
  IDOK checkVer
  Quit
  ;Check the version installed, compare it to the one user is trying to install
  checkVer:
   ${VersionCompare} $R2 ${PRODUCT_VERSION} $1 ;Version compare returns 0 if they are equal, 1 if version on the left is newer or 2 if version on the right is newer
   IntCmp $1 2 uninst
   IntCmp $1 0 uninst
    MessageBox MB_YESNO|MB_ICONQUESTION "A newer version $R2 seems to be already installed on your system.$\nWould you like to proceed with the installation of version ${PRODUCT_VERSION}?" \
        IDYES uninst
    Quit
  ;Run the uninstaller
  uninst:
    ExecWait '$R0 /S _?=$INSTDIR'
    RMDir /r "$R1\Drivers"
    Delete "$R1\${APP_NAME}.exe"
    Delete "$R1\ThemeControl.dll"
    Delete "$R1\${PRODUCT_PUBLISHER}.url"
    Delete $R0
    RMDir $R1
  done:
FunctionEnd

Function IsDotNetInstalled
  # Let's see if the user has the .NET Framework 4.5 installed on their system or not
  # Remember: you need Vista SP2 or 7 SP1.  It is built in to Windows 8, and not needed
  # In case you're wondering, running this code on Windows 8 will correctly return is_equal
  # or is_greater (maybe Microsoft releases .NET 4.5 SP1 for example)

  # Set up our Variables
  Var /GLOBAL dotNET45IsThere
  Var /GLOBAL dotNET_CMD_LINE
  Var /GLOBAL EXIT_CODE

  ReadRegDWORD $dotNET45IsThere HKLM "SOFTWARE\Microsoft\NET Framework Setup\NDP\v4\Full" "Release"
  IntCmp $dotNET45IsThere 378389 is_equal is_less is_greater

  is_equal:
    Goto done_compare_not_needed
  is_greater:
    # Useful if, for example, Microsoft releases .NET 4.5 SP1
    # We want to be able to simply skip install since it's not
    # needed on this system
    Goto done_compare_not_needed
  is_less:
    Goto done_compare_needed

  done_compare_needed:
    #.NET Framework 4.5 install is *NEEDED*
    # Microsoft Download Center EXE:
    # Web Bootstrapper: http://go.microsoft.com/fwlink/?LinkId=225704
    # Full Download: http://go.microsoft.com/fwlink/?LinkId=225702

    # Setup looks for components\dotNET45Full.exe relative to the install EXE location
    # This allows the installer to be placed on a USB stick (for computers without internet connections)
    # If the .NET Framework 4.5 installer is *NOT* found, Setup will connect to Microsoft's website
    # and download it for you

    # Reboot Required with these Exit Codes:
    # 1641 or 3010
    # Command Line Switches:
    # /showrmui /passive /norestart
    # Silent Command Line Switches:
    # /q /norestart
    # Let's see if the user is doing a Silent install or not
    IfSilent is_quiet is_not_quiet
    is_quiet:
        StrCpy $dotNET_CMD_LINE "/q /norestart"
        Goto do_local_install
    is_not_quiet:
        StrCpy $dotNET_CMD_LINE "/showrmui /passive /norestart"
        Goto do_local_install
    ;LookForLocalFile:
        # Let's see if the user stored the Full Installer
        ;IfFileExists "$EXEPATH\components\dotNET45Full.exe" do_local_install do_network_install
        do_local_install:
            # .NET Framework found on the local disk.  Use this copy
            ExecWait '"$INSTDIR\Drivers\NDP452-KB2901954-Web.exe" $dotNET_CMD_LINE' $EXIT_CODE ;if the .net is updated then this name needs to update also
            Goto is_reboot_requested
        # Now, let's Download the .NET
        ;do_network_install:
            ;Var /GLOBAL dotNetDidDownload
            ;NSISdl::download "http://go.microsoft.com/fwlink/?LinkId=225704" "$TEMP\dotNET45Web.exe" $dotNetDidDownload
            ;StrCmp $dotNetDidDownload success fail
            ;success:
                ;ExecWait '"$TEMP\dotNET45Web.exe" $dotNET_CMD_LINE' $EXIT_CODE
                ;Goto is_reboot_requested
            ;fail:
                ;MessageBox MB_OK|MB_ICONEXCLAMATION "Unable to download .NET Framework.  ${PRODUCT_NAME} will be installed, but will not function without the Framework!"
                ;Goto done_dotNET_function

            # $EXIT_CODE contains the return codes.  1641 and 3010 means a Reboot has been requested
         is_reboot_requested:
            ${If} $EXIT_CODE = 1641
            ${OrIf} $EXIT_CODE = 3010
                SetRebootFlag true
            ${EndIf}
  done_compare_not_needed:
    # Done dotNET Install
    DetailPrint  ".NET 4.5 or higher is installed."
    Goto done_dotNET_function
  #exit the function
  done_dotNET_function:
FunctionEnd
